group 'nl.martijndwars'
version "${version}"

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'net.researchgate.release'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "${artifactory_base_url}/plugins-release" }
        maven { url "${artifactory_base_url}/plugins-snapshot" }
    }

    dependencies {
        classpath 'net.researchgate:gradle-release:2.3.5'
    }
}

compileTestJava.options.encoding = 'ISO-8859-1'

jar {
    baseName = 'web-push'
    version = "${version}-SNAPSHOT"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(
          userName: project.hasProperty('ossrhUsername') ? ossrhUsername : '',
          password: project.hasProperty('ossrhPassword') ? ossrhPassword : ''
        )
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(
          userName: project.hasProperty('ossrhUsername') ? ossrhUsername : '',
          password: project.hasProperty('ossrhPassword') ? ossrhPassword : ''
        )
      }

      pom.project {
        name 'web-push'
        packaging 'jar'
        description 'A Web Push library for Java.'
        url 'https://github.com/MartijnDwars/web-push'

        scm {
          connection 'scm:git:git@github.com:MartijnDwars/web-push.git'
          developerConnection 'scm:git:git@github.com:MartijnDwars/web-push.git'
          url 'git@github.com:MartijnDwars/web-push.git'
        }

        licenses {
          license {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'martijndwars'
            name 'Martijn Dwars'
            email 'ikben@martijndwars.nl'
          }
        }
      }
    }
  }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "${artifactory_base_url}/libs-release" }
    maven { url "${artifactory_base_url}/libs-snapshot" }
    mavenLocal()
}


dependencies {
    // For making HTTP requests
    compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.2'

    // Not sure what for..
    compile group: 'com.google.guava', name: 'guava', version: '19.0'

    // For cryptographic operations
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.54'

    // For creating and signing JWT
    compile group: 'org.bitbucket.b_c', name: 'jose4j', version: '0.5.2'


    // For testing, obviously
    testCompile group: 'junit', name: 'junit', version: '4.11'

    // For parsing and unparsing JSON
    testCompile group: 'org.json', name: 'json', version: '20160212'

    // For turning InputStream to String
    testCompile group: 'commons-io', name: 'commons-io', version: '2.5'

    // For running the demo web-app that is used for Selenium tests
    testCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.0.M1'

    // For reading the demo vapid keypair from a pem file
    testCompile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.55'

    // For running Selenium tests
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-server', version: '3.0.0-beta3'

    // For downloading and linking WebDriver's for verious browsers
    testCompile group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '1.4.9'
}

publishing {
    repositories {
        maven {
            name 'Artifactory'
            url "${artifactory_base_url}/libs-release-local"

            credentials {
                username artifactory_user
                password artifactory_password
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId archivesBaseName
            from components.java
        }
    }
}

release {
    failOnCommitNeeded = false
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    revertOnFail = true
}

afterReleaseBuild.dependsOn publish

